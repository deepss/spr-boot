sourceSets {
  integrationTest {
    groovy {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir file('src/integration-test/groovy')
    }
    resources.srcDir file('src/integration-test/resources')
  }
}

configurations {
  integrationTestCompile.extendsFrom testCompile
  integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
  testClassesDir = sourceSets.integrationTest.output.classesDir
  classpath = sourceSets.integrationTest.runtimeClasspath
  outputs.upToDateWhen { false }
  group 'Integration test'
  description 'Runs the integration tests'
}


tasks.withType(Test) {
  reports.html.destination = file("${reporting.baseDir}/${name}")
}

test {
  outputs.dir snippetsDir
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test